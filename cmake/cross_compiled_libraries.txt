# ex: set syntax=cmake:

# TODO: improve (like dynamically download latest version)
if (from_bundle)
	include(ExternalProject)
	ExternalProject_Add(PrecompiledLibraries
			    PREFIX precompiled
			    URL https://github.com/hioa-cs/IncludeOS/releases/download/v0.9.0/IncludeOS_install_v0-9-0.tar.gz
			    URL_HASH SHA1=8f7dfa92d38814c0c4f3e83adacfbd741a617622
			    CONFIGURE_COMMAND ""
			    BUILD_COMMAND ""
			    UPDATE_COMMAND ""
			    INSTALL_COMMAND ""
	)

	set(PRECOMPILED_DIR ${CMAKE_CURRENT_BINARY_DIR}/precompiled/src/PrecompiledLibraries/)

	set(LIBCXX_INCLUDE_DIR ${PRECOMPILED_DIR}libcxx/include/)
	set(LIBCXX_LIB_DIR ${PRECOMPILED_DIR}/libcxx/)
	add_library(libcxx STATIC IMPORTED)
	set_target_properties(libcxx PROPERTIES IMPORTED_LOCATION ${LIBCXX_LIB_DIR}/libc++.a)

	set(NEWLIB_INCLUDE_DIR ${PRECOMPILED_DIR}/newlib/include/)
	set(NEWLIB_LIB_DIR ${PRECOMPILED_DIR}/newlib/)

	set(LIBGCC_LIB_DIR ${PRECOMPILED_DIR}/libgcc/)

	add_library(libc STATIC IMPORTED)
	set_target_properties(libc PROPERTIES IMPORTED_LOCATION ${NEWLIB_LIB_DIR}/libc.a)

	add_library(libm STATIC IMPORTED)
	set_target_properties(libm PROPERTIES IMPORTED_LOCATION ${NEWLIB_LIB_DIR}/libm.a)

	add_library(libgcc STATIC IMPORTED)
	set_target_properties(libgcc PROPERTIES IMPORTED_LOCATION ${LIBGCC_LIB_DIR}/libgcc.a)

	set(CRTEND ${PRECOMPILED_DIR}/crt/crtend.o)
	set(CRTBEGIN ${PRECOMPILED_DIR}/crt/crtbegin.o)
else(from_bundle)
	include(ExternalProject)

	set(binutils_version "2.26")
	set(TARGET "i686-elf")
	set(PREFIX "${CMAKE_CURRENT_BINARY_DIR}/IncludeOS_TEMP_install")
	set(num_jobs 4)
	ExternalProject_Add(binutils
			    PREFIX binutils
			    URL "ftp://ftp.gnu.org/gnu/binutils/binutils-${binutils_version}.tar.gz"
			    URL_HASH SHA1=bb933ec42eb2002d9270bd4c62a26c3706589ee9
			    CONFIGURE_COMMAND <SOURCE_DIR>/configure --target=${TARGET} --prefix=${PREFIX} --disable-nls --disable-werror
			    BUILD_COMMAND make -j ${num_jobs}
			    UPDATE_COMMAND ""
			    INSTALL_COMMAND make install
	)

	# add this for now, still working on performing all cross-compilation steps
	message(FATAL_ERROR "not fully supported yet")
endif(from_bundle)

# installation instructions
install(DIRECTORY ${LIBCXX_INCLUDE_DIR} DESTINATION include/libcxx
	FILES_MATCHING PATTERN "*.h")

install(DIRECTORY ${NEWLIB_INCLUDE_DIR} DESTINATION include/newlib
	FILES_MATCHING PATTERN "*.h")

install(FILES ${CRTEND} ${CRTBEGIN} DESTINATION share/includeos)

install(FILES ${NEWLIB_LIB_DIR}/libc.a ${NEWLIB_LIB_DIR}/libm.a ${LIBGCC_LIB_DIR}/libgcc.a DESTINATION lib)
